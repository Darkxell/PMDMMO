import com.github.spotbugs.SpotBugsTask

plugins {
    id("nebula.dependency-lock") version "7.3.0"

    id("pmd")
    // REVISIT: spotbugs classpath bs in 1.6.10
    // https://github.com/spotbugs/spotbugs-gradle-plugin/issues/119
    id("com.github.spotbugs") version "1.6.9"
    id("com.diffplug.gradle.spotless") version "3.18.0"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    version = "0.1.0-alpha"

    apply plugin: "pmd"
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.diffplug.gradle.spotless"

    tasks.withType(Pmd) {
        ignoreFailures = true
        ruleSetFiles = rootProject.files("config/pmd.xml")
        // ruleSets overrides ruleSetFiles if not empty. it's really stupid.
        ruleSets = []
    }

    tasks.withType(SpotBugsTask) {
        ignoreFailures = true
        // TODO: create spotbugs filter
//        includeFilter = rootProject.file("config/spotbugs.xml")
        reports {
            xml { enabled = false }
            html { enabled = true }
        }
    }

    spotless {
        java {
            removeUnusedImports()
            eclipse().configFile(rootProject.file("config/eclipse-format.xml"))
        }
    }
}
